apply plugin: 'android-library'
apply plugin: 'maven'
apply plugin: 'signing'

android {
    compileSdkVersion 17
    buildToolsVersion '17'

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            res.srcDirs = ['res']
        }
    }
}

task makeJavadoc (type: Exec) {
    commandLine (
            'javadoc',
            '-d', "${buildDir}/javadoc",
            '-sourcepath', './src/main/java',
            '-subpackages', 'com',
            '-encoding', 'UTF-8'
    )
    standardOutput = new ByteArrayOutputStream()
    errorOutput = new ByteArrayOutputStream()

    ext.output = {
        return standardOutput.toString()
    }
    ext.error = {
        return errorOutput.toString()
    }
}

task javadocJar (type : Jar, dependsOn: makeJavadoc) {
    classifier = 'javadoc'
    from "${buildDir}/javadoc"
}

task sourcesJar (type : Jar) {
    classifier = 'sources'
    from android.sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories.mavenDeployer {

        beforeDeployment { MavenDeployment deployment ->
            signPom(deployment)
        }

        repository (url: repoUrl) {
            authentication (
                    userName : sonatypeUsername,
                    password : sonatypePassword
            )
        }

        pom.project {
            name 'Android-NewPopupMenu'
            description 'Android-NewPopupMenu is an android library to create popup menu with GoogleMusic app-like style.'
            url projectUrl
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/license/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                url scmUrl
                connection "scm:git:${scmUrl}"
                developerConnection "scm:git:${scmUrl}"
            }
            developers {
                developer {
                    id 'u1aryz'
                    name 'Yuichi Arai'
                    email 'u1aryz.d@gmail.com'
                }
            }
        }
    }
}